traigase el "examples/matematicas.rj";

chamba mochao tri_mf(mochao val ,mochao inicio, mochao tope, mochao final){
    mochao lado_a = (val - inicio) / (tope - inicio);
    mochao lado_b = (final - val) / (final - tope);

    tornachile fonear max(fonear min(lado_a, lado_b), 0);
}

chamba mochao gauss_mf(mochao val, mochao desv, mochao media){
    mochao e = 2.71828;

    acabalado num = fonear exponente_n(val - media, 2);
    acabalado den = 2 * fonear exponente_n(desv , 2);

    acabalado div = fonear com_divisor(num, den);

    dizque(fonear modulo(num, den) == 0) tonces {
        tornachile 1 / fonear exponente_n(e, num / den);
    }
    pasino {
        //Los exponentes fraccionarios a√∫n no cuentan con soporte
        acabalado exp = num / div;
        acabalado raiz = den / div;

        acabalado factor = fonear piso(exp / raiz);

        mochao fraccionario = fonear exponente_n(fonear raiz_n(e, raiz), exp - (raiz * factor));

        tornachile 1 / (fonear exponente_n(e , factor) * fraccionario);
    }
}

acabalado servicio = 80;
acabalado comida = 80;

acabalado baja = 0;
acabalado media = 10;
acabalado alta = 30;

mochao s_pobre = fonear tri_mf(servicio, -1, 25, 50);
mochao s_aceptable = fonear tri_mf(servicio, 25, 50, 75);
mochao s_bueno = fonear tri_mf(servicio, 50, 75, 101);

mochao c_mala = fonear gauss_mf(comida, 15, 25);
mochao c_buena = fonear gauss_mf(comida, 15, 50);
mochao c_excelente = fonear gauss_mf(comida, 30, 100);

mochao r1 = fonear min(s_pobre, c_mala);
mochao r2 = fonear min(s_aceptable, c_mala);
mochao r3 = fonear min(s_bueno, c_mala);
mochao r4 = fonear min(s_pobre, c_buena);
mochao r5 = fonear min(s_pobre, c_mala);
mochao r6 = fonear min(s_bueno, c_excelente);
mochao r7 = fonear min(s_pobre, c_excelente);

mochao baja_ag = fonear max(fonear max(r1, r2), r4);
mochao media_ag = fonear max(fonear max(r3, r5), r7);
mochao alta_ag = r6;

mochao num = (baja * baja_ag) + (media * media_ag) + (alta * alta_ag);
mochao den = baja_ag + media_ag + alta_ag;

disir (num / den);